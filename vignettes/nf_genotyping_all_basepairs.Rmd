---
output: html_document
editor_options:
  chunk_output_type: console
css: highlight.css
---

```{r setup, include=FALSE}
knitr::knit_hooks$set(source = function(x, options) {
  if (!is.null(options$hilang)) {
      code_open <- "\n\n<div class=\"sourceCode\">\n<pre class=\"sourceCode\">\n<code class=\"sourceCode\">"
      code_close <- "\n</code>\n</pre>\n</div>\n"
      code_body <- highr::hi_andre(x, language = options$hilang, format = "html")
    stringr::str_c(
      code_open,
      knitr:::indent_block(paste(code_body, collapse = '\n'), ""),
      code_close
    )
  } else {
    stringr::str_c("\n\n```", tolower(options$engine), "\n",
                   paste(x, collapse = '\n'), "\n```\n\n")

  }
})
knitr::opts_chunk$set(echo = TRUE)
knitr::opts_knit$set(root.dir = '../')
```


```{r, include=FALSE}
source('R/draw_workflow.R')
prod_geno_all <- tibbler(c('filtered_snps'))
```

# (git 2) Genotyping II (all callable sites)

This pipeline can be executed as follows:

```sh
cd $BASE_DIR/nf/02_genotyping_all_basepairs
source ../../sh/nextflow_alias.sh
nf_run_allbp
```

## Summary

The genotyping procedure is controlled by the [**nextflow**](https://www.nextflow.io/) script `genotyping_all_basepairs.nf` (located under `$BASE_DIR/nf/02_genotyping_all_basepairs/`).
Based on an intermediate step from `genotyping.nf` ([git 1.10](git-1-genotyping-i-snps-only.html)), this script produces a data set that includes _all callable sites_  - that is SNPs as well a invariant sites that are covered by sequence.
Below is an overview of the steps involved in the genotyping process.
(The <span style="color:#4DAF4A">green dot</span> indicates the data input, <span style="color:#E41A1C">red arrows</span> depict output that is exported for further use.)

<div style="max-width:800px; margin:auto;">
```{r, echo = FALSE, warning = FALSE, message = FALSE}
girafe( ggobj = dot_plot(file = 'docs/genotyping_all_basepairs.dot', git = 2, point_types = prod_geno_all),
        width_svg = 9, height_svg = 9)

```
</div>

## Details of `genotyping_all_basepairs.nf`

### Data preparation

The nextflow script starts with a small header and then imports the joint genotyping likelihoods for all samples produced by `genotyping.nf`.

<div class="kclass">
```{r , eval = FALSE, hilang = 'nf'}
#!/usr/bin/env nextflow
// git 2.1
// open genotype likelyhoods
Channel
	.fromFilePairs("../../1_genotyping/1_gvcfs/cohort.g.vcf.{gz,gz.tbi}")
	.set{ vcf_cohort }
```

The genotyping of the different linkage groups is going to happen in parallel, so we need to initialize a channel for the 24 LGs.

```{r , eval = FALSE, hilang = 'nf'}
// git 2.2
// initialize LG channel
Channel
	.from( ('01'..'09') + ('10'..'19') + ('20'..'24') )
	.set{ ch_LG_ids }
```

The genotyping likelihoods are combined, effectively linking the data set to the 24 parallel LGs.

```{r , eval = FALSE, hilang = 'nf'}
// git 2.3
// combine genotypes and LGs
ch_LG_ids.combine( vcf_cohort ).set{ vcf_lg_combo }
```

The samples are jointly genotyped, independently for each LG and including invariant sites.

```{r , eval = FALSE, hilang = 'nf'}
// git 2.4
// actual genotyping step (including invariant sites)
process joint_genotype_snps {
	label "L_O88g90h_LGs_genotype"

	input:
	set val( lg ), vcfId, file( vcf ) from vcf_lg_combo

	output:
	set val( 'all' ), val( lg ), file( "all_site*.vcf.gz" ), file( "all_site*.vcf.gz.tbi" ) into all_bp_by_location

	script:
	"""
	gatk --java-options "-Xmx85g" \
		GenotypeGVCFs \
		-R=\$BASE_DIR/ressources/HP_genome_unmasked_01.fa \
		-L=LG${lg} \
		-V=${vcf[0]} \
		-O=intermediate.vcf.gz \
		--include-non-variant-sites=true

	gatk --java-options "-Xmx85G" \
		SelectVariants \
		-R=\$BASE_DIR/ressources/HP_genome_unmasked_01.fa \
		-V=intermediate.vcf.gz \
		--select-type-to-exclude=INDEL \
		-O=all_sites.LG${lg}.vcf.gz

	rm intermediate.*
	"""
}
```

The genotypes of the different LGs are merged.

```{r , eval = FALSE, hilang = 'nf'}
// git 2.5
// merge all LGs
process merge_genotypes {
	label 'L_78g5h_merge_genotypes'
	echo true

	input:
	set val( dummy ),  val( lg ), file( vcf ), file( tbi ) from all_bp_by_location.groupTuple()

	output:
	file( "all_sites.vcf.gz" ) into all_bp_merged

	script:
	"""
	INPUT=\$(ls -1 *vcf.gz | sed 's/^/ -I /g' | cat \$( echo ))

	gatk --java-options "-Xmx85g" \
		GatherVcfs \
		\$INPUT \
		-O=all_sites.vcf.gz
	"""
}
```

The genotypes are hard filtered based on various genotyping scores.

```{r , eval = FALSE, hilang = 'nf'}
// git 2.6
// quality based filtering
process filterSNP_first {
	label 'L_105g30h_filter_gt1'

	input:
	file( vcf ) from all_bp_merged

	output:
	set file( "intermediate.filterd.vcf.gz" ), file( "intermediate.filterd.vcf.gz.tbi" ) into filtered_snps_first

	script:
	"""
	module load openssl1.0.2

	tabix -p vcf ${vcf}

	gatk --java-options "-Xmx75G" \
		VariantFiltration \
		-R=\$BASE_DIR/ressources/HP_genome_unmasked_01.fa \
		-V ${vcf} \
		-O=intermediate.vcf.gz \
		--filter-expression "QD < 2.5" \
		--filter-name "filter_QD" \
		--filter-expression "FS > 25.0" \
		--filter-name "filter_FS" \
		--filter-expression "MQ < 52.0 || MQ > 65.0" \
		--filter-name "filter_MQ" \
		--filter-expression "MQRankSum < -0.2 || MQRankSum > 0.2" \
		--filter-name "filter_MQRankSum" \
		--filter-expression "ReadPosRankSum < -2.0 || ReadPosRankSum > 2.0 " \
		--filter-name "filter_ReadPosRankSum" \
		--QUIET true &> var_filt.log

	gatk --java-options "-Xmx75G" \
		SelectVariants \
		-R=\$BASE_DIR/ressources/HP_genome_unmasked_01.fa \
		-V=intermediate.vcf.gz \
		-O=intermediate.filterd.vcf.gz \
		--exclude-filtered \
		--QUIET true \
		--verbosity ERROR  &> var_select.log
	"""
}
```

A second filtering is based on the missingness of samples.

```{r , eval = FALSE, hilang = 'nf'}
// git 2.7
// missingness based filtering
// the resulting vcf file represents
// the 'all BP' data set
process filterSNP_second {
	label 'L_105g30h_filter_gt2'
	publishDir "../../1_genotyping/3_gatk_filtered/", mode: 'copy'

	input:
	set file( vcf ), file( tbi ) from filtered_snps_first

	output:
	file( "filterd.allBP.vcf.gz" ) into filtered_snps

	script:
	"""
	module load openssl1.0.2

	vcftools \
		--gzvcf ${vcf} \
		--max-missing-count 17 \
		--stdout  \
		--recode | \
		bgzip > filterd.allBP.vcf.gz
	"""
}
```
</div>

Finally, we are done with the second version of genotyping.

---
